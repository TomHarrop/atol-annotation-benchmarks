#!/usr/bin/env python3

import pandas as pd


def get_input_url(wildcards):
    my_manifest = annotation_manifest.loc[wildcards.organism_grouping_key]
    if wildcards.input_type == "genome":
        url = my_manifest["genome_url"]
    elif wildcards.input_type == "annotation":
        # TODO: check multiple ext
        url = my_manifest["annot_url"]
    else:
        raise ValueError(f"Unknown input_type {input_type}")
    return url


def select_busco_lineage(wildcards):
    my_manifest = annotation_manifest.loc[wildcards.organism_grouping_key]
    busco_lineage = (
        "resources/busco_databases/"
        f"{my_manifest.busco_dataset_name}_{busco_odb_verion}"
    )
    return busco_lineage


def select_taxid(wildcards):
    return annotation_manifest.loc[wildcards.organism_grouping_key].taxon_id


# expected output

qc_filenames = [
    "omark_summary.json",
    "proteins.faa",
    "short_summary.specific.busco.json",
    "short_summary.specific.busco.txt",
]

annotation_manifest_file = Path("config", "annotation_manifest.csv")
annotation_manifest = pd.read_csv(
    annotation_manifest_file, index_col="organism_grouping_key"
)

# which BUSCO datasets do we need
busco_odb_verion = "odb10"
busco_dataset_names = sorted(set(annotation_manifest["busco_dataset_name"]))
busco_lineage_names = {k: f"{k}_{busco_odb_verion}" for k in busco_dataset_names}

# which datasets are we using
all_grouping_keys = sorted(set(annotation_manifest.index))


include: "rules/download_busco_dataset.smk"


wildcard_constraints:
    lineage="|".join(busco_lineage_names.values()),
    organism_grouping_key="|".join(all_grouping_keys),
    input_type="|".join(["genome", "annotation"]),
    ext="|".join(sorted(set(annotation_manifest["format"])) + ["fa"]),


rule annotation_qc:
    input:
        annotation="results/annooddities/{organism_grouping_key}/AnnoOddities.gff",
        busco_lineage=select_busco_lineage,
        db="data/omark/LUCA.h5",
        ete_ncbi_db="data/omark/ete/taxa.sqlite",
        fasta="resources/genome/{organism_grouping_key}.fa",
    output:
        [
            Path("results/annotation_qc/{organism_grouping_key}", x).as_posix()
            for x in qc_filenames
        ],
    log:
        "logs/annotation_qc/{organism_grouping_key}.log",
    params:
        lineage_dataset=subpath(input.busco_lineage, basename=True),
        lineages_path=subpath(input.busco_lineage, parent=True),
        mem_gb=lambda wildcards, resources: int(resources.mem_mb / 1000),
        outdir=subpath(output[0], parent=True),
        taxid=select_taxid,
    threads: 16
    resources:
        mem="64GB",
        runtime=60,
    container:
        "docker://quay.io/biocontainers/atol-qc-annotation:0.1.4--pyhdfd78af_0"
    shell:
        "atol-qc-annotation "
        "--threads {threads} "
        "--mem {params.mem_gb} "
        "--fasta {input.fasta} "
        "--annot {input.annotation} "
        "--lineage_dataset {params.lineage_dataset} "
        "--lineages_path {params.lineages_path} "
        "--db {input.db} "
        "--taxid {params.taxid} "
        "--ete_ncbi_db {input.ete_ncbi_db} "
        "--outdir {params.outdir} "
        "--logs {params.outdir}/logs "
        "&> {log}"


rule annooddities:
    input:
        annotation="resources/annotation/{organism_grouping_key}.gff3",
        fasta="resources/genome/{organism_grouping_key}.fa",
    output:
        stats="results/annooddities/{organism_grouping_key}/AnnoOddities.combined_statistics.json",
        gff="results/annooddities/{organism_grouping_key}/AnnoOddities.gff",
        archive="results/annooddities/{organism_grouping_key}/AnnoOddities.tar.gz",
    params:
        outdir=subpath(output["stats"], parent=True),
    log:
        "logs/annooddities/{organism_grouping_key}.log",
    benchmark:
        "logs/annooddities/{organism_grouping_key}.stats"
    resources:
        mem="8GB",
        runtime=20,
    container:
        "docker://gemygk/annooddities:v0.1.0"
    shadow:
        "minimal"
    shell:
        "annooddities "
        "--genome_fasta {input.fasta} "
        "--gff3_file {input.annotation} "
        "--output_prefix {wildcards.organism_grouping_key} "
        "&> {log} "
        "&& "
        "cp {wildcards.organism_grouping_key}.AnnoOddities.combined_statistics.json {output.stats} "
        "&& "
        "cp {wildcards.organism_grouping_key}.AnnoOddities.gff {output.gff} "
        "&& "
        'tar -cvf {output.archive} -I "gzip --best" '
        "./{wildcards.organism_grouping_key}.* ./oddity_files &>> {log}"


rule download_galaxy_file:
    output:
        file="resources/{input_type}/{organism_grouping_key}.{ext}",
    params:
        url=get_input_url,
    log:
        "logs/download_galaxy_file/{input_type}.{organism_grouping_key}.{ext}.log",
    resources:
        time=10,
    container:
        "docker://quay.io/biocontainers/gnu-wget:1.18--hb829ee6_10"
    shell:
        "wget {params.url} -O {output} &> {log}"


rule target:
    default_target: True
    input:
        expand(
            "results/annotation_qc/{organism_grouping_key}/omark_summary.json",
            organism_grouping_key=all_grouping_keys,
        ),
        expand(
            "results/annooddities/{organism_grouping_key}/AnnoOddities.combined_statistics.json",
            organism_grouping_key=all_grouping_keys,
        ),
